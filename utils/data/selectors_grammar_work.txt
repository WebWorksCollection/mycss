<selector-list> = <complex-selector-list>

<complex-selector-list> = <complex-selector>#

<compound-selector-list> = <compound-selector>#

<simple-selector-list> = <simple-selector>#

<relative-selector-list> = <relative-selector>#

<complex-selector> = <compound-selector> [ <combinator>? <compound-selector> ]*

<relative-selector> = <combinator>? <complex-selector>

<combinator> = <'>'> <'>' ws> | <'>' ws> | <'+' ws> | <'~' ws> | <column-token ws>

<compound-selector> = <simple-selector>+

<simple-selector> = <type-selector> | <id-selector> |
                    <class-selector> | <attribute-selector> |
                    <pseudo-class-selector> | <pseudo-element-selector>

<type-selector> = <wq-name> | <ns-prefix> <'*'>

<ns-prefix> = <ident-token>? <'|'>

<wq-name> = <ns-prefix>? <ident-token ws>

<id-selector> = <hash-token ws>

<class-selector> = <'.'> <ident-token ws>

<attribute-selector> = <[-token ws> <wq-name> <]-token ws> |
                       <[-token ws> <wq-name> <attr-matcher> [ <string-token ws> | <ident-token ws> ] <attr-modifier>? <]-token ws>

<attr-matcher> = <'=' ws> | <include-match-token ws> | <dash-match-token ws> | <prefix-match-token ws> |
                 <suffix-match-token ws> | <substring-match-token ws>

<attr-modifier> = <'i' ws>

<pseudo-class-selector> = <':'> <ident-token ws> |
                          <':'> <function-token> <any-value> <)-token ws>

<pseudo-element-selector> = <':'> <pseudo-class-selector>
